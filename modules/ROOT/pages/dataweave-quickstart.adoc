= DataWeave Quickstart
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]
:keywords: studio, anypoint, transform, transformer, format, xml, json, metadata, dataweave, data weave, datamapper, dwl, dfl, dw, output structure, input structure, map, mapping

In this quickstart, you run DataWeave scripts on sample data, without relying
on external data sources. The examples introduce some key DataWeave concepts
and show how to turn the Transform Message component into a DataWeave
playground.

To run the scripts, you copy them into the source code area of a Transform
Message component in xref:7.3@studio::index.adoc[Studio], then view the results
in the component's *Preview* pane. You also create data in files that you
load into the component so that your DataWeave script can act on that content.
Once you feel comfortable with DataWeave examples here and elsewhere in the
documentation, you can try out your own DataWeave scripts on sample data in
the playground.

[[example1]]
.Transform Message Component in a Studio Project
image::dw-tut-script-studio-basic.png[Simple Script]

The figure shows a Transform Message component in the center canvas (within *testingscriptFlow*). Below the Studio canvas, the *Transform Message* tab
includes a DataWeave script in the source code area with output in the
*Preview* pane.

== Prerequisites

xref:7.3@studio::to-download-and-install-studio.adoc[Studio 7] is required.
Versions 7.3 or 7.2.3 are recommended. Other Studio 7 versions are untested
with this quickstart.

Once you are done, you need to set up a project with a Transform Message component. See <<setup>>.

[[setup]]
== Set Up a Studio Project

Set up a project that serves as a DataWeave playground:

. In Studio, click *File* -> *New* -> *Mule Project* to create a Mule project.
. Provide a name for the project, and click *Finish*.
. From the *Mule Palette* tab of your new project, click *Core*, and then drag
the *Transform Message* component into the Studio canvas, as shown in <<example1>>.
. [Optional] In the *Transform Message* tab, click the _empty_ rectangle next
to *Preview* to expand the source code area, and then click *Preview* to open
the Preview pane.
+
[[studio_source_only]]
.Studio Preview and Source Only View Selectors
image::studio_source_only.png[Studio Preview and Source Only View Selectors]
+
. Proceed to <<start_scripting>>.

[[start_scripting]]
== Start Scripting

. Begin with a simple DataWeave script that outputs the value of the DataWeave
variable `myJson`:
.. Add this script to the source code area of the *Transform Message* tab, as shown in <<example1>>:
+
[source,dw,linenums]
----
%dw 2.0
var myJson = { "hello" : "world" }
output application/json
---
myJson
----
+
The JSON object `{ "hello" : "world" }`, defined as the `myJson` variable
in the script's header, is a data structure made up of one or more key-value
pairs within curly braces. Learn about
xref:dataweave-language-introduction.adoc[DataWeave Scripts]
and xref:dataweave-variables.adoc[DataWeave Variables].
+
.. Preview the output:
+
[source,json,linenums]
----
{
  "hello": "world"
}
----
+
. Now try an example that uses the DataWeave `avg` function in a DataWeave
variable (`myJson`) to get averages of two sets of numbers.
+
.. Replace the current script in the source code area with this one:
+
[source,dw,linenums]
----
%dw 2.0
var myJson = {
  a: avg([1, 1000]),
  b: avg([1, 2, 3])
}
output application/json
---
myJson
----
+
The `avg` functions here get invoked when you add `myJson` to the body of the
script, producing the calculated averages within the JSON object, which
you can see in the *Preview* pane. The structures `[1, 1000]` and `[1, 2, 3]`
are xref:dataweave-types#dw_type_array[arrays]. Learn more about
xref:dw-core-functions-avg.adoc[avg] and
xref:dataweave-types.adoc[DataWeave types].
+
.. Preview the output:
+
[source,json,linenums]
----
{
  "a": 500.5,
  "b": 2.0
}
----
+
.This Example in Studio
image::dw-tut-script-studio-var.png[Script Using var]
+
. Now try a more complicated script that reads XML input, transforms it to
JSON, and only selects the contents of the `prices` element.
+
.. Replace the current script in the source code area with this one:
+
[source,dw,linenums]
----
%dw 2.0
var myRead = read("<prices><basic>9.99</basic></prices>",
                "application/xml",
                { indexedReader: "false" })
output application/json
---
{
   "JSON" : myRead.prices
}
----
+
If you encounter an issue previewing this example, try changing `myRead.prices`
to `myRead."prices"`. Learn more about the xref:dw-core-functions-read.adoc[read]
function, xref:dataweave-formats.adoc[Formats Supported by DataWeave], and
xref:dataweave-selectors.adoc[DataWeave Selectors].
+
.. Preview the output:
+
[source,json,linenums]
----
{
  "JSON": {
    "basic": "9.99"
  }
}
----
+
. [[readurl_ex]] Now use `readUrl` to read the contents of a file in the Studio
`src/main/resources` folder.
+
.. Add a file by right-clicking the `src/main/resources` folder, then
navigating to *New* -> *File*, providing the file name `myJson.json` for
that file, and clicking *Finish*.
.. From `src/main/resources`, double-click your new `myJson.json` file, and
then provide and save the following sample content in the *myJson.json* tab
(in Studio 7.3, within the *Source* sub-tab of the *myJson.json* tab):
+
[source,json,linenums]
----
{
  "hello": "world"
}
----
+
.myJson.json in Studio
image::dw-tut-sample-file.png[DataWeave Sample File]
+
.. Returning to the Transform Message component, replace the current script with one that uses
`readUrl` to get the JSON contents of your file:
+
[source,dw,linenums]
----
%dw 2.0
output application/json
---
readUrl("classpath://myJson.json", "application/json")
----
+
Learn more about the xref:dw-core-functions-readurl.adoc[readUrl] function.
+
.. View the matching output in the *Preview* pane.
+
[source,json,linenums]
----
{
  "hello": "world"
}
----
+
Note that you can also load the payload of a file through a custom data type in
the Transform Message component. That procedure is covered later, in
<<med_large_payload>>.
+
. Now map elements within an array to keys and values of an object.
.. [[input_map]] Replace the current script in the source code area with this one:
+
[source,dw,linenums]
----
%dw 2.0
output application/json
---
{
  (
    ["a", "b", "c"] map ((value, index) -> {
        (index): value
    })
  )
}
----
+
The `map` function iterates over the array on the left to apply the lambda
(anonymous function) on the right to elements in that array. The lambda uses
xref:dataweave-functions-lambdas.adoc#_use_named_parameters_in_a_lambda[named parameters] (`value` and `index`) to select the values and indices from the
array and populate a DataWeave object with key-value pairs. Learn about
xref:dw-core-functions-map.adoc[map], and compare `map` with xref:dw-core-functions-mapobject.adoc[mapObject], which takes an object as
input.
+
.. [[output_map]] Preview the output:
+
[source,json,linenums]
----
{
  "0": "a",
  "1": "b",
  "2": "c"
}
----
+
.. Now run `pluck` on the <<output_map, output>> of the `map` example:
+
[source,dw,linenums]
----
%dw 2.0
output application/json
---
{
  "0": "a",
  "1": "b",
  "2": "c"
} pluck ((value) -> value)
----
+
This example iterates over the values in the input object and outputs them into
an array that matches the <<input_map, input>> from the `map` example. Learn more about xref:dw-core-functions-pluck.adoc[pluck].
+
.. Preview the output:
+
[source,json,linenums]
----
[
  "a",
  "b",
  "c"
]
----
+
. Now try a complex example that maps and merges fields from items in separate arrays.
.. Replace the current script in the source code area with this one:
+
[source,dw,linenums]
----
%dw 2.0
var myVar = [
  { bookId: 101,
    title: "world history",
    price: "19.99"
  },
  {
    bookId: 202,
    title: 'the great outdoors',
    price: "15.99"
  }
]
var myVar2 = [
  {
    bookId: 101,
    author: "john doe"
  },
  {
    bookId: 202,
    author: "jane doe"
  }
]
output application/json
---
myVar map (item, index) -> using (id = item.bookId) {
	"id" : id,
	"topic" : item.title,
	"cost" : item.price as Number,
	(myVar2 filter ($.*bookId contains id) map (item) -> {
		author : item.author
	})
}
----
+
Learn more about the xref:dw-core-functions-filter.adoc[filter] function
(used to get the `author` values from the array in the `myVar2` variable),
xref:dataweave-types-coercion.adoc[Type Coercion with DataWeave] (for
`as Number`), and
xref:dataweave-variables#example_local_variable[Examples: Local DataWeave Variables] (for `using (id = item.bookId)`).
+
.. Preview the output:
+
[source,json,linenums]
----
[
  {
    "id": "101",
    "topic": "world history",
    "cost": 19.99,
    "author": "john doe"
  },
  {
    "id": "202",
    "topic": "the great outdoors",
    "cost": 15.99,
    "author": "jane doe"
  }
]
----
. Proceed to <<more_examples>>.

[[more_examples]]
== Try Out More DataWeave Examples

Now you are ready to run DataWeave examples on your own. You can run them
whenever you have time to discover more about the DataWeave language.

. Find many more examples to try out in Studio here:
+
 * xref:dw-functions.adoc[DataWeave Reference] (provides many examples
that use functions provided by DataWeave function modules)
 * xref:dw-operators.adoc[DataWeave Operators]
 * xref:dataweave-flow-control.adoc[Flow Control in DataWeave]
 * xref:dataweave-pattern-matching.adoc[Pattern Matching in DataWeave]
 * xref:dataweave-cookbook.adoc[DataWeave Cookbook]
 * xref:dataweave-functions.adoc[Define DataWeave Functions]
+
. For help running the examples that act on a `payload`, go to
<<provide_payload>>.
. Proceed to <<next_steps>>.

[[provide_payload]]
=== Run Examples that Act on a Payload

In DataWeave, `payload` is a Mule Runtime variable that holds the content of a
xref:about-mule-message#_message_payload[Mule message]. To try out DataWeave
examples from the documentation that act on a `payload` (such as those in
xref:dataweave-pattern-matching.adoc[Pattern Matching in DataWeave]), you have several options:

* <<small_payload>>: You add the payload to a variable in the header of the
DataWeave script.
* <<med_large_payload>>: You add the payload through a file.

Alternatives that require a running Mule app (such as using
xref:set-payload-transformer-reference[Set Payload]) are not covered here.

[[small_payload]]
==== Run Examples with Small Payloads

For examples with small amounts of data, you can convert the input payload to a variable. You simply copy the payload's content into a DataWeave variable
(`var`) in the header of a DataWeave script. Then, in the body of the script,
you replace `payload` with the name of the new variable to reference that
variable.

. [[payload_to_var]] Start with this script in the Output area:
+
[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
ContainsRequestedItem: payload.root.*order.*items contains "3"
----
+
Notice that you cannot preview it yet:
+
image::dw-tut-payload-example.png[Script Without Input Payload]
+
Learn more about the xref:dw-core-functions-contains.adoc[contains] function
and about the built-in `payload` variable, in
xref:dataweave-variables-context.adoc[Mule Runtime variables].
+
. [[modify_payload]] Now modify this example so that it uses some sample input
instead of attempting to use `payload` to input content of a Mule message that
does not exist:
+
.. Add a `myInput` DataWeave variable supplying some input data the script can
read, and change the Mule `payload` variable in the body of the script to the
DataWeave `myInput` variable.
+
[source,DataWeave, linenums]
----
%dw 2.0
var myInput = read("<root>
    <order>
      <items>1</items>
      <items>3</items>
    </order>
    <order>
      <items>2</items>
    </order>
</root>",
"application/xml")
output application/json
---
ContainsRequestedItem: myInput.root.*order.*items contains "3"
----
.. Preview the results:
+
[source,json,linenums]
----
{
  "ContainsRequestedItem": true
}
----

[[med_large_payload]]
==== Run Examples with Medium to Large Payloads

For DataWeave examples with medium and large amounts of data, you can create a
data type for the content of a local file that contains your data. You can then load that content into Transform Message and use a DataWeave script to act on it. Follow these steps to load the `src/main/resources/myJson.json` content
(created in the <<readurl_ex, readUrl>> example) as `payload` of the Transform
component:

. Replace the current content from the source code area of the Transform
Message with a script that selects the payload of the input:
+
[source,dw,linenums]
----
%dw 2.0
output application/json
---
payload
----
+
. Notice that you cannot preview a payload now because it does not exist yet.
+
image::dw-tut-payload-example2.png[Script Without Input Payload]
+
. Now provide a simple JSON object as the payload of the Transform Message:
+
.. In the *Transform Message* tab, click the left-most rectangle from the
*Preview* button, shown here: <<studio_source_only>>.
+
If you mouse over that icon, you can see the label *Show Graphic*.
+
.. In the left-most column of the *Transform Message* tab, click the top
*Define metadata* link for an unknown payload (*Payload: Unknown*).
+
image::studio-define-payload.png[Define MetaData in Studio]
+
. Now load the contents of `myJson.json` (created in
the <<readurl_ex, readUrl>> example) into the Transform Message component:
+
.. Click *+Add* to open the *Create new type* dialog.
.. In the dialog, provide the *Type id* `myJsonType`, and click *Create type*.
.. Now select *JSON* from the *Type* drop-down menu.
.. Below the new type, change *Schema* to *Example*, use navigation button
to find `src/main/resources/myJson.json`, and click *Open* to display the
structure of the file contents (a DataWeave object) into the
*Select metadata type* window.
+
image::studio_select_metadata_type.png[Select Metadata Type]
+
.. Now click *Select* to load the object into the payload of the
Transform Message component.
.. Click the *payload* tab at the bottom of the screen to see the contents of
the file.
+
[source,json,linenums]
----
{
  "hello": "world"
}
----
+
.. Click the Preview button in the tab to see the JSON object as output.
.. Now revise the DataWeave script slightly to select the value of the payload
instead of the entire object: `payload.hello`.
.. Notice that the payload is now `"world"`.
+
. Alternative:
.. Create a file with the content you want to use.
.. Read that content into a DataWeave variable (as shown in the
<<readurl_ex, readUrl>> example).
.. Replace the `payload` variable with the DataWeave variable name.

[[next_steps]]
== Next Steps

* To get started with Mule app development and data mapping, see the
xref:mule-app-tutorial.adoc[Mule App Development Tutorial].
* Beyond Transform Message, many Mule connectors, modules, and Core components
accept short DataWeave expressions in their *fx* fields. To learn about them,
you can start with xref:mule-application-about.adoc[Mule Applications].
* To try out on your sample data in running a Mule app, you can use these
Core components:
xref:set-payload-transformer-reference.adoc[Set Payload] to provide
content for the payload of a Mule event,
xref:variable-transformer-reference.adoc[Set Variable] to create content in a
Mule event variable, and the xref:scheduler-concept.adoc[Scheduler] to trigger
the regular generation of Mule events to hold any data set by those components,
for example:
+
image::studio-hello-world.png[Hello World in Studio]
