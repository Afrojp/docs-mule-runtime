= DataWeave Language Quickstart
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]
:keywords: studio, anypoint, transform, transformer, format, xml, json, metadata, dataweave, data weave, datamapper, dwl, dfl, dw, output structure, input structure, map, mapping

To practice and learn about the DataWeave language, you can try out many
of the DataWeave examples in this documentation by copying the DataWeave
scripts into the *Output* pane of the Transform Message component in Studio
and viewing the results in the component's *Preview* pane. Once you feel comfortable with examples below, you can try your own DataWeave scripts on
sample data.

[[example1]]
.Transform Message Component in the Studio UI
image::dw-tut-script-studio-basic.png[Simple Script]

NOTE: This section simply shows an easy way to test DataWeave scripts with
sample data. It describes a _limited use_ of the Transform Message component to help you get started with the language (as opposed to Mule app development) and does not attempt to cover every DataWeave use case. For more information, see xref:transform-component-about.adoc[Transform Message], and note that many Mule components, connectors, and modules (see xref:mule-application-about.adoc[Mule Applications]) accept short DataWeave expressions in their *fx* fields.

. Add the Transform Message component to a new Studio project:
.. In Studio, click *File* -> *New* -> *Mule Project* to create a Mule project.
.. In the Mule Palette of your new project, click *Core*, and then drag the
*Transform Message* component into the Studio canvas, as shown in <<example1>>.
.. Double-click the component to open its edit area in the *Transform Message*
tab.
+
To expand the *Output* and *Preview* areas of the component, you can click the
empty rectangle and *Preview* at the top-right of the tab.
+
. Now try this basic script, which simply outputs the value of the variable
`myJson`.
.. Add this script to the Output pane, as shown in <<example1>>:
+
[source,dw,linenums]
----
%dw 2.0
var myJson = { "hello" : "world" }
output application/json
---
myJson
----
+
.. Preview the output:
+
[source,json,linenums]
----
{
  "hello": "world"
}
----
+
For details, see xref:dataweave-variables.adoc[DataWeave Variables]
and xref:dataweave-language-introduction.adoc[DataWeave Scripts].
+
. Now try an example that uses the DataWeave `avg` function in a variable
(`myJson`) to get averages for two sets of numbers.
+
image::dw-tut-script-studio-var.png[Script Using var]
+
The `avg` functions get invoked when you add `myJson` to the body of the
script, producing the calculated averages within the JSON structure, which
you can see in the *Preview* pane.
+
.. Add this script to the Output pane:
+
[source,dw,linenums]
----
%dw 2.0
var myJson = {
  a: avg([1, 1000]),
  b: avg([1, 2, 3])
}
output application/json
---
myJson
----
+
.. Preview the output:
+
[source,json,linenums]
----
{
  "a": 500.5,
  "b": 2.0
}
----
+
For details, see the xref:dw-core-functions-avg.adoc[avg] function.
+
. Now try a more complicated script that reads XML input, transforms it to
JSON, and only selects the contents of the `prices` element.
+
.. Add this script to the Output pane:
+
[source,dw,linenums]
----
%dw 2.0
var myRead = read("<prices><basic>9.99</basic></prices>",
                "application/xml",
                { indexedReader: "false" })
output application/json
---
{
   "JSON" : myRead."prices"
}
----
+
.. Preview the output:
+
[source,json,linenums]
----
{
  "JSON": {
    "basic": "9.99"
  }
}
----
+
For details, see the xref:dw-core-functions-read.adoc[read] function,
xref:dataweave-formats.adoc[Formats Supported by DataWeave], and
xref:dataweave-selectors.adoc[DataWeave Selectors].
+
[[modify_readurl]]
. Now use `readUrl` to read the contents of a file in the Studio
`src/main/resources` folder.
+
.. Add a file by right-clicking the `src/main/resources` folder, then navigating to *New* -> *File*, providing the file name `myJSON.json` for
that file, and clicking *Finish*.
.. From `src/main/resources`, double-click your new `myJSON.json` file, and
then provide and save the following sample content in the *myJSON.json* tab:
+
[source,json,linenums]
----
{
  "hello": "world"
}
----
+
image::dw-tut-sample-file.png[DataWeave Sample File]
+
.. Returning to Transform Message component, provide a simple script that uses
`readUrl` to get the JSON contents of your file.
+
[source,dw,linenums]
----
%dw 2.0
output application/json
---
readUrl("classpath://myJson.json", "application/json")
----
+
For details, see the xref:dw-core-functions-readurl.adoc[readUrl] function.
+
.. View the matching output in the *Preview* pane.
+
[source,json,linenums]
----
{
  "hello": "world"
}
----
+
. Now zip and unzip some arrays that are defined in separate variables.
.. Add this script to the Output pane:
+
[source,dw,linenums]
----
%dw 2.0
var array1 = [0, 1, 2]
var array2 = ["a", "b"]
var array3 = [[0,"a"],[1,"b"]]
output application/json
---
{
  "zip1" : zip(array1, array2),
  "zip2" : zip(array2, array1),
  "unzip1" : unzip(array3)
}
----
+
For details, see the xref:dw-core-functions-zip.adoc[zip] and
xref:dw-core-functions-unzip.adoc[unzip] functions.
+
.. Preview the output:
+
[source,json,linenums]
----
{
  "zip1": [
    [ 0, "a" ],
    [ 1, "b" ]
  ],
  "zip2": [
    [ "a", 0 ],
    [ "b", 1 ]
  ],
  "unzip1": [
    [ 0, 1 ],
    [ "a", "b" ]
  ]
}
----
. Now try a complex example that maps and merges fields from items in separate arrays:
.. Add this script to the Output pane:
+
[source,dw,linenums]
----
%dw 2.0
var myVar = read('[
  { "bookId":"101",
    "title":"world history",
    "price":"19.99"
  },
  {
    "bookId":"202",
    "title":"the great outdoors",
    "price":"15.99"
  }]',"application/json")
var myVar2 = read('[
  {
    "bookId":"101",
    "author":"john doe"
  },
  {
    "bookId":"202",
    "author":"jane doe"
  }
]',"application/json")
output application/json
---
myVar map (item, index) -> using (id = item.bookId) {

	"id" : id,
	"topic" : item.title,
	"cost" : item.price as Number,
	(myVar2 filter ($.*bookId contains id) map (item) -> {
		author : item.author
	})
}
----
+
For details, see the xref:dw-core-functions-map.adoc[map] and
xref:dw-core-functions-filter.adoc[filter] functions,
xref:dataweave-types-coercion.adoc[Type Coercion with DataWeave] (for
`as Number`), and xref:dataweave-variables#example_local_variable[Examples: Local DataWeave Variables] (for `using (id = item.bookId)`). And note the use
of named parameters (such as `item`) in the anonymous function
`(item) -> { author : item.author }`); see
xref:dataweave-functions-lambdas.adoc#_use_named_parameters_in_a_lambda[Use Named Parameters in a Lambda].
+
.. Preview the output:
+
[source,json,linenums]
----
[
  {
    "id": "101",
    "topic": "world history",
    "cost": 19.99,
    "author": "john doe"
  },
  {
    "id": "202",
    "topic": "the great outdoors",
    "cost": 15.99,
    "author": "jane doe"
  }
]
----
+
. Now try to modify an example so that you can run it with sample data in the Transform Message component:
.. Start with this example in the Output area, noting that you cannot preview it yet:
+
[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
ContainsRequestedItem: payload.root.*order.*items contains "3"
----
+
For details, see the xref:dw-core-functions-contains.adoc[contains] function, and read about the built-in `payload` variable in xref:dataweave-variables-context.adoc[Mule Runtime variables].
+
image::dw-tut-payload-example.png[Script Without Input Payload]
+
[[modify_payload]]
.. Now modify this example so that it uses some sample input instead of
attempting to use an input `payload`:
+
* Add a `myInput` variable that supplies an input data the script can read.
* Change the `payload` variable in the body of the script to the `myInput`
variable.
+
[source,DataWeave, linenums]
----
%dw 2.0
var myInput = read("<root>
    <order>
      <items>1</items>
      <items>3</items>
    </order>
    <order>
      <items>2</items>
    </order>
</root>",
"application/xml")
output application/json
---
ContainsRequestedItem: payload.root.*order.*items contains "3"
----

To find many more examples to try in Studio, see these sections:

* xref:dw-functions.adoc[DataWeave Reference] (provides many examples
that use functions provided by DataWeave function modules)
* xref:dataweave-flow-control.adoc[Flow Control in DataWeave]
* xref:dataweave-pattern-matching.adoc[Pattern Matching in DataWeave]
* xref:dataweave-cookbook.adoc[DataWeave Cookbook]
* xref:dataweave-functions.adoc[Define DataWeave Functions]

NOTE: To run examples that use the `payload` variable, you can provide the sample input data through a variable (`var`) in the header of your DataWeave script (as shown <<modify_payload, the previous step>>) or from a file that you read into a variable, as shown in the <<ex_readurl, `readUrl`>> example, then
use then replace `payload` with the variable name.
