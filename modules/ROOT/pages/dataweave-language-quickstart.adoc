= DataWeave Language Quickstart
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]
:keywords: studio, anypoint, transform, transformer, format, xml, json, metadata, dataweave, data weave, datamapper, dwl, dfl, dw, output structure, input structure, map, mapping

To practice and learn about the DataWeave language, you can try out most
of the DataWeave examples in this documentation by copying the DataWeave
scripts into the *Output* pane of the Transform Message component in Studio
and viewing the results in the component's *Preview* pane.

[[example1]]
.Transform Message Component in the Studio UI
image::dw-script-studio-basic.png[Simple Script]

Once you feel comfortable with examples below, you can try your own DataWeave
scripts on sample data. (Note that this section simply shows a way to test your
scripts and only describes a _limited use case_ for the component, and it does
not cover important uses of the built-in
xref:dataweave-variables-context.adoc[Mule Runtime variables] that DataWeave can handle. For more complete information about the component, see xref:transform-component-about.adoc[Transform Message].)

. In Studio, click *File* -> *New* -> *Mule Project* to create a Mule project.
. In the Mule Palette of your new project, click *Core*, and then drag the
*Transform Message* component into the Studio canvas.
. Double-click the component to open its edit area in the *Transform Message*
tab.
+
To expand the *Output* and *Preview* areas of the component, you can click the empty rectangle and *Preview* at the top-right of the tab.
+
. Now try this basic script, which simply outputs the value of the variable
`myJson`.
.. Add this script to the Output pane (also shown in <<example1>>):
+
[source,dw,linenums]
----
%dw 2.0
var myJson = { "hello" : "world" }
output application/json
---
myJson
----
+
.. Preview the output:
+
[source,json,linenums]
----
{
  "hello": "world"
}
----
+
For details, see xref:dataweave-variables.adoc[DataWeave Variables]
and xref:dataweave-language-introduction.adoc[DataWeave Scripts].
+
. Now try an example that uses the DataWeave `avg` function in a variable
(`myJson`) to get averages for two sets of numbers.
+
image::dw-script-studio-var.png[Script Using var]
+
The `avg` functions get invoked when you add `myJson` to the body of the script, producing the calculated averages within the JSON structure.
+
.. Add this script to the Output pane:
+
[source,dw,linenums]
----
%dw 2.0
var myJson = {
  a: avg([1, 1000]),
  b: avg([1, 2, 3])
}
output application/json
---
myJson
----
+
.. Preview the output:
+
[source,json,linenums]
----
{
  "a": 500.5,
  "b": 2.0
}
----
+
For details, see the xref:dw-core-functions-avg.adoc[avg] function.
+
. Now try a more complicated script that reads XML input, transforms it to
JSON, and only selects the contents of the `prices` element.
+
.. Add this script to the Output pane:
+
[source,dw,linenums]
----
%dw 2.0
var myRead = read("<prices><basic>9.99</basic></prices>",
                "application/xml",
                { indexedReader: "false" })
output application/json
---
{
   "JSON" : myRead."prices"
}
----
+
.. Preview the output:
+
[source,json,linenums]
----
{
  "JSON": {
    "basic": "9.99"
  }
}
----
+
For details, see the xref:dw-core-functions-read.adoc[read] function,
xref:dataweave-formats.adoc[Formats Supported by DataWeave], and
xref:dataweave-selectors.adoc[DataWeave Selectors].
+
. Now use `readUrl` to read the contents of a file in the Studio `src/main/resources` folder.
+
.. Add a file with this JSON content by right-clicking the
`src/main/resources` folder, then navigating to *New* -> *File* and providing
a the file name `myJSON.json` for that file.
.. Double-click your new `myJSON.json` file within `src/main/resources`, and then provide and save the following sample content to the file:
+
[source,json,linenums]
----
{
  "hello": "world"
}
----
+
image::dataweave-sample-file.png[DataWeave Sample File]
+
.. Returning to Transform Message component, provide a simple script that uses `readUrl` to get the JSON contents of your file.
+
[source,dw,linenums]
----
%dw 2.0
output application/json
---
readUrl("classpath://myJson.json", "application/json")
----
+
For details, see the xref:dw-core-functions-readUrl.adoc[readUrl] function.
+
.. View the matching output in the *Preview* pane.
+
[source,json,linenums]
----
{
  "hello": "world"
}
----
+
. Now zip and unzip some arrays that are defined in separate variables.
.. Add this script to the Output pane:
+
[source,dw,linenums]
----
%dw 2.0
var array1 = [0, 1, 2]
var array2 = ["a", "b"]
var array3 = [[0,"a"],[1,"b"]]
output application/json
---
{
  "zip1" : zip(array1, array2),
  "zip2" : zip(array2, array1),
  "unzip1" : unzip(array3)
}
----
+
For details, see the xref:dw-core-functions-zip.adoc[zip] and
xref:dw-core-functions-unzip.adoc[unzip] functions.
+
.. Preview the output:
+
[source,json,linenums]
----
{
  "zip1": [
    [ 0, "a" ],
    [ 1, "b" ]
  ],
  "zip2": [
    [ "a", 0 ],
    [ "b", 1 ]
  ],
  "unzip1": [
    [ 0, 1 ],
    [ "a", "b" ]
  ]
}
----
. Now try a complex example that maps and merges fields from items in separate arrays:
.. Add this script to the Output pane:
+
[source,dw,linenums]
----
%dw 2.0
var myVar = read('[
  { "bookId":"101",
    "title":"world history",
    "price":"19.99"
  },
  {
    "bookId":"202",
    "title":"the great outdoors",
    "price":"15.99"
  }]',"application/json")
var myVar2 = read('[
  {
    "bookId":"101",
    "author":"john doe"
  },
  {
    "bookId":"202",
    "author":"jane doe"
  }
]',"application/json")
output application/json
---
myVar map (item, index) -> using (id = item.bookId) {

	"id" : id,
	"topic" : item.title,
	"cost" : item.price as Number,
	(myVar2 filter ($.*bookId contains id) map (item) -> {
		author : item.author
	})
}
----
+
.. Preview the output:
+
[source,json,linenums]
----
[
  {
    "id": "101",
    "topic": "world history",
    "cost": 19.99,
    "author": "john doe"
  },
  {
    "id": "202",
    "topic": "the great outdoors",
    "cost": 15.99,
    "author": "jane doe"
  }
]
----
+
For details, see the xref:dw-core-functions-map.adoc[map] and
xref:dw-core-functions-filter.adoc[filter] functions,
xref:dataweave-types-coercion.adoc[Type Coercion with DataWeave], and, for
`using`, xref:dataweave-variables#example_local_variable[Examples: Local DataWeave Variables].
+
. To find many more examples to try in Studio, see these sections:
+
* xref:dataweave-flow-control.adoc[Flow Control in DataWeave]
* xref:dataweave-pattern-matching.adoc[Pattern Matching in DataWeave]
* xref:dw-functions.adoc[DataWeave Reference] (provides many examples that use functions within the DataWeave function modules)
* xref:dataweave-cookbook.adoc[DataWeave Cookbook]
* xref:dataweave-functions.adoc[Define DataWeave Functions]
